buildscript {
    repositories {
        google()
        jcenter()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:3.4.1'
    }

}


def generateVersionNumberString = { ->
    StringBuilder stringBuilder = new StringBuilder()
    stringBuilder.append((new Date()).format('yyMMddHHmm'))
    return stringBuilder.toString()
}

def generateTimestamp = { ->
    StringBuilder stringBuilder = new StringBuilder()
    stringBuilder.append((new Date()).getTime())
    stringBuilder.append("L")
    return stringBuilder.toString()
}

def generateRandomUUID = { ->
    StringBuilder stringBuilder = new StringBuilder();
    stringBuilder.append('"' + UUID.randomUUID().toString() + '"')
    return stringBuilder.toString()
}

def generateVersionName = { ->

    StringBuilder stringBuilder = new StringBuilder()
    try {
        def stdout = new ByteArrayOutputStream()
        exec {
            commandLine 'git', 'describe', '--always'
            standardOutput = stdout
        }
        String commitObject = stdout.toString().trim()
        try {
            stdout = new ByteArrayOutputStream()
            exec {
                commandLine 'git', 'describe', '--tags'
                standardOutput = stdout
            }
            //stringBuilder.append(stdout.toString().trim())
            //stringBuilder.append("-")
        } catch (ignored) {
            // no tags
        }

        stringBuilder.append(commitObject)
        stdout = new ByteArrayOutputStream()
        exec {
            commandLine 'git', 'rev-parse', '--show-toplevel'
            standardOutput = stdout
        }
        if (stdout.toString().trim().contains("xDrip-Experimental")) {
            stringBuilder.append("-experimental")
        }

        stdout = new ByteArrayOutputStream()
        exec {
            commandLine 'git', 'rev-parse', '--abbrev-ref', 'HEAD'
            standardOutput = stdout
        }
        String branch = stdout.toString().trim()
        if (!branch.equals("master")) {
            stringBuilder.append('-')
            stringBuilder.append(branch)
        }

    } catch (ignored) {
        return "NoGitSystemAvailable"
    }
    stringBuilder.append('-')
    stringBuilder.append((new Date()).format('yyyy.MM.dd'))
    return stringBuilder.toString()

}

apply plugin: 'com.android.application'
//apply plugin: 'me.tatarka.retrolambda'
//apply plugin: 'io.fabric'

android {
    compileSdkVersion 29
    lintOptions {
        checkReleaseBuilds false
//        abortOnError false
    }
    defaultConfig {
        applicationId "com.eveningoutpost.dexdrip"
        minSdkVersion 21
        //noinspection ExpiredTargetSdkVersion
        targetSdkVersion 23
        versionCode 1
        versionName generateVersionName()
        buildConfigField "int", "buildVersion", generateVersionNumberString()
        buildConfigField "String", "buildUUID", generateRandomUUID()
        buildConfigField "long", "buildTimestamp", generateTimestamp()
        buildConfigField "int", "targetSDK", targetSdkVersion.mApiLevel.toString()

        applicationId = "com.eveningoutpost.dexdrip"
        resValue "string", "label_xdrip", "XDrip"
        resValue "string", "label_xdrip_large", "XDrip(Large)"
        resValue "string", "label_xdrip_big_chart", "XDrip(BigChart)"
        resValue "string", "label_xdrip_circle", "XDrip(Circle)"
        resValue "string", "label_xdrip_activity", "XDrip Prefs."
        resValue "string", "app_settings", "XDrip Settings"

    }

    // The defaultConfig values above are fixed, so your incremental builds don't
    // need to rebuild the manifest (and therefore the whole APK, slowing build times).
    // But for release builds, it's okay. So the following script iterates through
    // all the known variants, finds those that are "release" build types, and
    // changes those properties to something dynamic.
    applicationVariants.all { variant ->
        if (variant.buildType.name == "release") {
            variant.outputs.each { output ->
                output.versionNameOverride = generateVersionName()
            }
        }
    }

    testOptions {
        unitTests {
            includeAndroidResources = true
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    packagingOptions {
        exclude 'META-INF/INDEX.LIST'
    }

    dataBinding {
        enabled = true
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    
    flavorDimensions "version"
    productFlavors {
        // When building a variant that uses this flavor, the following configurations
        // override those in the defaultConfig block.
        fast {
            // To avoid using legacy multidex when building from the command line,
            // set minSdkVersion to 21 or higher. When using Android Studio 2.3 or higher,
            // the build automatically avoids legacy multidex when deploying to a device running
            // API level 21 or higherâ€”regardless of what you set as your minSdkVersion.
            minSdkVersion 21
            versionNameSuffix "-dev"
            buildConfigField "int", "buildVersion", "2021010100"
            buildConfigField "String", "buildUUID", "\"0f79a60a-5616-99be-8eb1-a430edcfd9fe\""
            buildConfigField "long", "buildTimestamp", "1609459200L"
            resConfigs "en", "xxhdpi"
        }

        prod {
            // If you've configured the defaultConfig block for the release version of
            // your app, you can leave this block empty and Gradle uses configurations in
            // the defaultConfig block instead. You still need to create this flavor.
            // Otherwise, all variants use the "dev" flavor configurations.
        }
    }
}

allprojects {
    repositories {
        google()
        jcenter()
        flatDir {
            dirs 'libs'
        }
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    compileOnly 'com.google.android.wearable:wearable:2.5.0'
    implementation 'com.google.android.support:wearable:2.5.0'
    implementation 'com.android.support:appcompat-v7:26.1.0'
    // implementation 'com.google.android.support:wearable:1.4.0'
    //implementation 'com.google.android.gms:play-services-wearable:9.4.0'
    // FIX upgrage to play-services-wearable:17.0.0 requires upgrade to AndroidX libraries, https://developers.google.com/android/guides/releases#june_17_2019
    implementation 'com.google.android.gms:play-services-wearable:10.2.1'
    implementation 'com.squareup.wire:wire-runtime:2.2.0'
    //implementation 'com.ustwo.android:clockwise-wearable:1.0.2'
    //implementation files('libs/hellocharts-library-1.5.5.jar')
    implementation(name: 'ustwo-clockwise-debug', ext: 'aar')
    implementation 'me.denley.wearpreferenceactivity:wearpreferenceactivity:0.5.0'
    implementation 'com.google.code.gson:gson:2.8.6'


    //implementation 'io.reactivex:rxjava:1.3.2'
    implementation 'com.activeandroid:thread-safe-active-android:3.1.1'
    implementation 'com.google.guava:guava:24.1-jre'
    implementation 'io.reactivex:rxjava:1.3.3'
    implementation 'com.polidea.rxandroidble2:rxandroidble:1.11.1'
    implementation 'org.apache.commons:commons-math3:3.6'
    testImplementation "org.robolectric:robolectric:4.2.1"
    testImplementation 'junit:junit:4.12'
    testImplementation "com.google.truth:truth:0.39"

    implementation 'uk.com.robust-it:cloning:1.9.5'
    // you will want to install the android studio lombok plugin
    compileOnly 'org.projectlombok:lombok:1.18.10'
//    compileOnly 'javax.annotation:javax.annotation-api:1.3.1'
    annotationProcessor "org.projectlombok:lombok:1.18.10"
}
