buildscript {
    repositories {
        maven { url 'https://maven.fabric.io/public' }
    }

    dependencies {
        classpath 'io.fabric.tools:gradle:1.+'
    }
}
apply plugin: 'com.android.application'
apply plugin: 'me.tatarka.retrolambda'
//apply plugin: 'io.fabric'

//def AAVersion = '4.3.1'
//dependencies {
//    annotationProcessor "org.androidannotations:androidannotations:$AAVersion"
//    compile "org.androidannotations:androidannotations-api:$AAVersion"
//}


repositories {
    maven {
        url "https://maven.google.com"
    }
    maven { url 'https://maven.fabric.io/public' }
    maven {
        url "https://raw.github.com/embarkmobile/zxing-android-minimal/mvn-repo/maven-repository/"
    }
    maven {
        url "https://jitpack.io"
    }
    maven { url "https://oss.sonatype.org/content/groups/public/" }
    flatDir {
        dirs 'libs'
    }
}

def generateVersionNumber = { ->
    StringBuilder stringBuilder = new StringBuilder();
    stringBuilder.append((new Date()).format('yyMMddHHmm'))
    return Integer.parseInt(stringBuilder.toString());
}

def generateVersionNumberString = { ->
    StringBuilder stringBuilder = new StringBuilder();
    stringBuilder.append((new Date()).format('yyMMddHHmm'))
    return stringBuilder.toString();
}

def generateVersionName = { ->

    StringBuilder stringBuilder = new StringBuilder();
    try {
        def stdout = new ByteArrayOutputStream()
        exec {
            commandLine 'git', 'describe', '--always'
            standardOutput = stdout
        }
        String commitObject = stdout.toString().trim()
        try {
            stdout = new ByteArrayOutputStream()
            exec {
                commandLine 'git', 'describe', '--tags'
                standardOutput = stdout
            }
            //stringBuilder.append(stdout.toString().trim())
            //stringBuilder.append("-")
        } catch (ignored) {
            // no tags
        }

        stringBuilder.append(commitObject)
        stdout = new ByteArrayOutputStream()
        exec {
            commandLine 'git', 'rev-parse', '--show-toplevel'
            standardOutput = stdout
        }
        if (stdout.toString().trim().contains("xDrip-Experimental")) {
            stringBuilder.append("-experimental")
        }

        stdout = new ByteArrayOutputStream()
        exec {
            commandLine 'git', 'rev-parse', '--abbrev-ref', 'HEAD'
            standardOutput = stdout
        }
        String branch = stdout.toString().trim()
        if (!branch.equals("master")) {
            stringBuilder.append('-')
            stringBuilder.append(branch)
        }

    } catch (ignored) {
        return "NoGitSystemAvailable";
    }
    stringBuilder.append('-')
    stringBuilder.append((new Date()).format('yyyy.MM.dd'))
    return stringBuilder.toString()

}



android {
    compileSdkVersion 26
    buildToolsVersion '26.0.2'


    defaultConfig {
        applicationId "com.eveningoutpost.dexdrip"
        minSdkVersion 18
        // increasing target SDK version can cause compatibility issues with Android 7+
        targetSdkVersion 23
        // change versionCode only when downgrade should be prevented
        // eg, when data structures are incompatible
        versionCode 1603091400
        versionName generateVersionName()
        buildConfigField "int", "buildVersion", generateVersionNumberString()
        vectorDrawables.useSupportLibrary = true
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    testOptions {
        animationsDisabled = true
    }

    lintOptions {
        disable 'MissingTranslation'
        disable 'ExtraTranslation'
    }

    packagingOptions {
        exclude 'META-INF/INDEX.LIST'
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    dataBinding {
        enabled = true
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'),
                    'proguard-rules.pro'
        }
        debug {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'),
                    'proguard-rules.pro'
            testProguardFile 'proguard-debug.pro'
            ext.enableCrashlytics = false
            //      applicationIdSuffix ".debug"
            versionNameSuffix "-debug"
            debuggable true
        }
    }
}

// auto test rig
task AutoInstall(type: Exec) {
    commandLine "bash", "$projectDir/autoinstallviausb"
}

// auto tester
tasks.whenTaskAdded { task ->
    if (task.name == 'assembleRelease') {
        if (System.getenv('USERDOMAIN') == "PC-PC") {
            task.finalizedBy 'AutoTestRig'
            AutoTestRig.outputs.upToDateWhen { false }
        }
    }
}

task AutoTestRig() << {
    AutoInstall.execute()
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    wearApp project(':wear')
    //weapApp files('../wear/build/outputs/apk/wear_release.apk')
    //testCompile 'com.squareup.okhttp:mockwebserver:2.5.0'
    compile('com.github.nightscout:android-uploader:CORE_FOR_XDRIP') {
        transitive = false;
    }

    compile project(':localeapi')
    //compile 'com.nispok:snackbar:2.10.8'
    compile 'com.android.support:appcompat-v7:26.0.2'
    compile 'com.android.support:design:26.0.2'
    compile 'com.android.support:support-v13:26.0.2'
    compile 'com.android.support:recyclerview-v7:26.0.2'
    compile 'com.android.support:cardview-v7:26.0.2'
    //compile 'com.google.android.gms:play-services-drive:9.4.0'
    compile 'com.google.firebase:firebase-messaging:9.4.0'
    compile 'com.google.android.gms:play-services-maps:9.4.0'
    compile 'com.google.android.gms:play-services-wearable:9.4.0'
    compile 'com.google.android.gms:play-services-location:9.4.0'
    compile "com.google.android.gms:play-services-gcm:9.4.0"
    compile 'com.squareup.wire:wire-runtime:2.2.0'
    compile 'com.squareup.okhttp:okhttp:2.7.5'
    compile 'com.squareup.okhttp3:okhttp:3.5.0'
    compile 'com.squareup.okhttp3:logging-interceptor:3.5.0'
    //compile 'com.jakewharton.retrofit:retrofit1-okhttp3-client:1.1.0'
    compile 'com.squareup.retrofit2:retrofit:2.1.0'
    compile 'com.squareup.retrofit2:converter-moshi:2.1.0'
    compile 'com.squareup.moshi:moshi:1.4.0'
    //compile 'org.influxdb:influxdb-java:2.6'
    compile 'com.google.code.gson:gson:2.4'
    compile 'com.squareup.retrofit:converter-gson:2.0.0-beta2'
    compile 'com.squareup.retrofit:retrofit:2.0.0-beta2'
    compile 'com.getpebble:pebblekit:3.1.0'
    compile 'io.reactivex:rxjava:1.3.0'
    //compile 'com.activeandroid:thread-safe-active-android:3.1.1'
    compile 'com.github.lecho:hellocharts-android:v1.5.8'
    compile "com.polidea.rxandroidble:rxandroidble:1.4.1"
    compile 'com.google.guava:guava:20.0'
    compile 'com.embarkmobile:zxing-android-minimal:2.0.0@aar'
    compile 'com.embarkmobile:zxing-android-integration:2.0.0@aar'
    //compile 'com.embarkmobile:zxing-android-legacy:2.0.0@aar'
    compile 'com.google.zxing:core:3.1.0'
    compile 'net.tribe7.seeds:seeds-functional:16.0.1'
    compile 'net.tribe7.seeds:seeds-primitives:16.0.1'
    compile 'org.slf4j:slf4j-api:1.7.7'
    compile 'org.apache.commons:commons-math3:3.6'
    compile('com.crashlytics.sdk.android:crashlytics:2.6.6@aar') {
        transitive = true;
    }
    compile 'uk.com.robust-it:cloning:1.9.5'
    compile 'com.github.amlcurran.showcaseview:library:5.4.3'
    // compile 'com.itextpdf:itextg:5.5.9'

    // Pebble-Trend, needs to be checked if really needed
    //compile 'com.android.support:multidex:1.0.1'
    compile 'ar.com.hjg:pngj:2.1.0'
    compile 'com.bugfender.sdk:android:0.+'
    // compile 'com.rarepebble:colorpicker:1.7.0'
    compile(name: 'colorpicker', ext: 'aar')
    compile 'com.google.dagger:dagger:2.11'
//    //compile 'com.google.dagger:dagger-android-support:2.x' // if you use the support libraries
    annotationProcessor 'com.google.dagger:dagger-compiler:2.11'

    testCompile 'junit:junit:4.12'
    testCompile 'org.objenesis:objenesis:2.5.1'
    testCompile 'org.hamcrest:hamcrest-library:1.3'
    testCompile "org.robolectric:robolectric:3.4.2"


    androidTestCompile('com.schibsted.spain:barista:1.7.0') {
        exclude group: 'com.android.support'
    }
    // espresso, workarounds needed for correct functioning
    androidTestCompile 'com.android.support.test.espresso:espresso-core:3.0.1', {
        exclude group: 'com.android.support', module: 'support-annotations'
    }
    androidTestCompile 'com.android.support.test:runner:1.0.1', {
        exclude group: 'com.android.support', module: 'support-annotations'
    }
    androidTestCompile 'com.android.support.test.espresso:espresso-idling-resource:3.0.1'
    // add this for intent mocking support
    //androidTestCompile 'com.android.support.test.espresso:espresso-intents:3.0.1'
    // add this for webview testing support
    //androidTestCompile 'com.android.support.test.espresso:espresso-web:3.0.1'

}

apply plugin: 'com.google.gms.google-services'
